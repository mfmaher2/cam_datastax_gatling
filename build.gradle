plugins {
    id 'java'
    id 'scala'

    //gatling gradle plugin
    // 0.7.4,the max version of the plugin that works with gatling2.3 that dse libs support
    //this one is the unofficial version before it was forked to be official version with 3.4
    //https://plugins.gradle.org/plugin/com.github.lkishalmi.gatling/0.7.4
    id "com.github.lkishalmi.gatling" version "0.7.4"

}


configurations {
    externalLib
}

//gatling gradle plugin config reference: https://github.com/ysb33r/gradle-gatling-plugin
gatling {

    logLevel = 'WARN' // logback root level
    //systemProperties = ['file.encoding': 'UTF-8', 'java.net.preferIPv6Addresses': true ] // not supported in v0.7.4
    jvmArgs = ['-server', '-Xmx1G',
               '-XX:+HeapDumpOnOutOfMemoryError',
               '-XX:+UseG1GC',
               '-XX:+ParallelRefProcEnabled',
               '-XX:MaxInlineLevel=20',
               '-XX:MaxTrivialSize=12',
               '-XX:-UseBiasedLocking'
    ]

}

dependencies {

    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.16'
    annotationProcessor group: 'com.datastax.oss', name: 'java-driver-mapper-processor', version: '4.13.0'
    annotationProcessor group: 'org.mapstruct', name: 'mapstruct-processor', version: '1.4.2.Final'
    annotationProcessor group: 'org.projectlombok', name: 'lombok-mapstruct-binding', version: '0.2.0'


    implementation 'org.scala-lang:scala-library:2.12.8'
    implementation 'com.datastax.oss:java-driver-core:4.13.0'
    implementation 'com.datastax.oss:java-driver-mapper-processor:4.13.0'
    implementation 'com.datastax.oss:java-driver-mapper-runtime:4.13.0'
    implementation 'com.datastax.oss:java-driver-query-builder:4.13.0'
    implementation 'org.mapstruct:mapstruct-processor:1.4.2.Final'
    implementation 'org.mapstruct:mapstruct:1.4.2.Final'
    implementation 'org.projectlombok:lombok:1.18.16'
    implementation 'org.projectlombok:lombok-mapstruct-binding:0.2.0'
    implementation 'uk.co.jemos.podam:podam:7.2.7.RELEASE'
    implementation 'com.github.javafaker:javafaker:1.0.2'

    gatling 'com.datastax.oss:java-driver-core:4.13.0'
    gatling 'com.datastax.oss:java-driver-mapper-processor:4.13.0'
    gatling 'com.datastax.oss:java-driver-mapper-runtime:4.13.0'
    gatling 'com.datastax.oss:java-driver-query-builder:4.13.0'
    gatling 'org.mapstruct:mapstruct-processor:1.4.2.Final'
    gatling 'org.mapstruct:mapstruct:1.4.2.Final'
    gatling 'org.projectlombok:lombok:1.18.22'
    gatling 'org.projectlombok:lombok-mapstruct-binding:0.2.0'
    gatling 'uk.co.jemos.podam:podam:7.2.7.RELEASE'
    gatling 'org.scala-lang:scala-library:2.12.16'
    gatling files('externalLib/gatling-dse-stress-assembly-1.3.7.jar')

}

repositories {
    mavenCentral()
}


configurations {
    fatJarDependencies.extendsFrom gatling
}


jar {
    manifest {
        attributes(
                'Main-Class': 'com.datastax.gatling.stress.Starter'
        )
    }
}

task fatJar(type: Jar) {
    manifest.from jar.manifest
    archiveClassifier = 'all'   //string gets appended to the jar name
    from files(sourceSets.main.output.classesDirs)
    from files(sourceSets.gatling.output)
    from { configurations.fatJarDependencies.collect { it.isDirectory() ? it : zipTree(it) } } {
        exclude 'META-INF/*.SF'
        exclude 'META-INF/*.DSA'
        exclude 'META-INF/*.RSA'
    }
    with jar
}



